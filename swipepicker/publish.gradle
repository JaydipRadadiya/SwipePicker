apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/swipepicker-release.aar")
            groupId 'one.xcorp.widget'
            artifactId 'swipe-picker'
            version versionName

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = findProperty('user')
    key = findProperty('key')
    publications = ['Production']
    configurations = ['archives']

    pkg {
        userOrg = 'xcorp'
        repo = 'android'
        name = 'swipe-picker'

        version {
            name = versionName
            vcsTag = versionName
            desc = versionDescription
            released = new Date()
        }
    }
}
